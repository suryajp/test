buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.ajoberstar:gradle-git:1.4.2"
    }
}

apply plugin: org.ajoberstar.gradle.git.ghpages.GithubPagesPlugin
apply from: "$rootDir/gradle/publishing-local.gradle"

task generateSampleBuild(type: Copy) {
    ext.outputDir = file("$buildDir/sample")
    from file("$rootDir/gradle/samples/java-project.txt")
    into outputDir
    rename 'java-project.txt', 'build.gradle'
    expand(projectVersion: '0.1', repoDir: "$buildDir/repo")
}

task generateSampleSite(type: GradleBuild) {
    dependsOn generateSampleBuild, 'publishPluginMavenPublicationToFileBasedRepository'
    dir = generateSampleBuild.outputDir
    tasks = ['generateSite']
}

ext {
    repoPath = 'gradle-guides/gradle-site-plugin'
    ghToken = System.getenv('GH_TOKEN')
}

githubPages {
    if (ghToken) {
        repoUri = "https://github.com/${repoPath}.git"
        credentials {
            username = ghToken
            password = ''
        }
    } else {
        repoUri = "git@github.com:${repoPath}.git"
    }

    pages {
        from(file("$generateSampleBuild.outputDir/build/docs/site")) {
            into 'sample'
        }
    }
}

publishGhPages.dependsOn generateSampleSite